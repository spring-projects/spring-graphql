pluginManagement {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'org.springframework.boot') {
                useModule("org.springframework.boot:spring-boot-gradle-plugin:${requested.version}")
            }
        }
    }
}

plugins {
	id "com.gradle.enterprise" version "3.16.2"
	id "io.spring.ge.conventions" version "0.0.15"
}

rootProject.name = 'spring-graphql'
include 'platform',
	'spring-graphql',
	'spring-graphql-test',
	'spring-graphql-docs'

settings.gradle.projectsLoaded {
	gradleEnterprise {
		buildScan {
			File buildDir = settings.gradle.rootProject.getBuildDir()
			buildDir.mkdirs()
			new File(buildDir, "build-scan-uri.txt").text = "(build scan not generated)"
			buildScanPublished { scan ->
				if (buildDir.exists()) {
					new File(buildDir, "build-scan-uri.txt").text = "${scan.buildScanUri}\n"
				}
			}
		}
	}
}
