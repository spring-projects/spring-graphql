description = "Spring for GraphQL"

ext {
	moduleProjects = [project(":spring-graphql"), project(":spring-graphql-test")]
	springFrameworkVersion = "6.1.21"
	graphQlJavaVersion = "22.3"
	springBootVersion = "3.3.8"
}

subprojects {
	apply plugin: 'org.springframework.graphql.build.conventions'
	group = 'org.springframework.graphql'

	ext.javadocLinks = [
			"https://docs.oracle.com/en/java/javase/17/docs/api/",
			"https://javadoc.io/doc/com.graphql-java/graphql-java/${graphQlJavaVersion}/",
			"https://docs.spring.io/spring-framework/docs/${springFrameworkVersion}/javadoc-api/",
			"https://docs.spring.io/spring-boot/${springBootVersion}/api/java/",
	] as String[]

	repositories {
		mavenCentral()
		maven {
			url "https://repo.spring.io/milestone"
			content {
				includeGroup "org.springframework.boot"
			}
		}
		if (version.contains('-')) {
			maven { url "https://repo.spring.io/milestone" }
		}
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/snapshot" }
		}
	}
}

configure(moduleProjects) {
	apply plugin: 'java-library'
	apply plugin: 'java-test-fixtures'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		dependencyManagement {
			canBeConsumed = false
			canBeResolved = false
			visible = false
		}
		matching { it.name.endsWith("Classpath") }.all { it.extendsFrom(dependencyManagement) }
	}

	dependencies {
		dependencyManagement(enforcedPlatform(dependencies.project(path: ":platform")))
	}

	components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
	components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }
	
	apply from: "${rootDir}/gradle/publishing.gradle"
}
