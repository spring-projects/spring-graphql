plugins {
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

ext {
	moduleProjects = [project(":spring-graphql"), project(":spring-graphql-test")]
}

description = "Spring GraphQL"

subprojects {
	apply plugin: 'io.spring.dependency-management'

	group = 'org.springframework.graphql'

	repositories {
		mavenCentral()
		if (version.contains('-')) {
			maven { url "https://repo.spring.io/milestone" }
		}
		if (version.endsWith('-SNAPSHOT')) {
			maven { url "https://repo.spring.io/snapshot" }
		}
	}
}

configure(moduleProjects) {
	apply plugin: 'java-library'

	java {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}

	dependencyManagement {
		imports {
			mavenBom "com.fasterxml.jackson:jackson-bom:2.12.3"
			mavenBom "io.projectreactor:reactor-bom:2020.0.7"
			mavenBom "org.springframework:spring-framework-bom:5.3.7"
			mavenBom "org.springframework.data:spring-data-bom:2021.0.1"
			mavenBom "org.springframework.security:spring-security-bom:5.5.1"
			mavenBom "org.junit:junit-bom:5.7.2"
		}
		dependencies {
			dependency 'com.graphql-java:graphql-java:16.2'
			dependency 'org.assertj:assertj-core:3.19.0'
			dependencySet(group: 'org.apache.logging.log4j', version: '2.14.1') {
				entry 'log4j-api'
				entry 'log4j-core'
				entry 'log4j-jul'
				entry 'log4j-slf4j-impl'
			}
		}
		generatedPomCustomization {
			enabled = false
		}
	}

	apply from: "${rootDir}/gradle/publishing.gradle"
}
